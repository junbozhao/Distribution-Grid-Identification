Clear 

!
! This script is based on a script developed by Tennessee Tech Univ students
! Tyler Patton, Jon Wood, and David Woods, April 2009
!

new circuit.IEEE13Nodeckt 
~ basekv=115 pu=1.0001 phases=3 bus1=SourceBus  
~ Angle=30                                                         ! advance angle 30 deg so result agree with published angle
~ MVAsc3=20000 MVASC1=21000    ! stiffen the source to approximate inf source



!SUB TRANSFORMER DEFINITION 
! Although this data was given, it does not appear to be used in the test case results
! The published test case starts at 1.0 per unit at Bus 650. To make this happen, we will change the impedance
! on the transformer to something tiny by dividing by 1000 using the DSS in-line RPN math
New Transformer.Sub Phases=3 Windings=2   XHL=(8 1000 /)
~ wdg=1 bus=SourceBus   conn=delta  kv=115  kva=5000   %r=(.5 1000 /)  XHT=4
~ wdg=2 bus=650         conn=wye    kv=4.16 kva=5000   %r=(.5 1000 /)  XLT=4

! FEEDER 1-PHASE VOLTAGE REGULATORS
! Define low-impedance 2-wdg transformer

New Transformer.Reg1 phases=1 XHL=0.01 kVAs=[1666 1666]
~ Buses=[650.1 RG60.1] kVs=[2.4  2.4] %LoadLoss=0.01
new regcontrol.Reg1  transformer=Reg1 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 

New Transformer.Reg2 phases=1 XHL=0.01 kVAs=[1666 1666]
~ Buses=[650.2 RG60.2] kVs=[2.4  2.4] %LoadLoss=0.01
new regcontrol.Reg2  transformer=Reg2 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 

New Transformer.Reg3 phases=1 XHL=0.01 kVAs=[1666 1666]
~ Buses=[650.3 RG60.3] kVs=[2.4  2.4] %LoadLoss=0.01
new regcontrol.Reg3  transformer=Reg3 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9 


!TRANSFORMER DEFINITION 
New Transformer.XFM1  Phases=3   Windings=2  XHL=2
~ wdg=1 bus=633       conn=Wye kv=4.16     kva=500    %r=.55   XHT=1
~ wdg=2 bus=634       conn=Wye kv=0.480    kva=500    %r=.55   XLT=1


!LINE CODES
redirect IEEELineCodes.dss

// these are local matrix line codes
// corrected 9-14-2011
New linecode.mtx601 nphases=3 BaseFreq=60 
~ rmatrix = (0.3465 | 0.1560 0.3375 | 0.1580 0.1535 0.3414 ) 
~ xmatrix = (1.0179 | 0.5017 1.0478 | 0.4236 0.3849 1.0348 ) 
~ normamps = 730
~ emergamps = 1095
~ units=mi 
New linecode.mtx602 nphases=3 BaseFreq=60 
~ rmatrix = (0.7526 | 0.1580 0.7475 | 0.1560 0.1535 0.7436 ) 
~ xmatrix = (1.1814 | 0.4236 1.1983 | 0.5017 0.3849 1.2112 ) 
~ normamps = 340
~ emergamps = 510
~ units=mi 
New linecode.mtx603 nphases=2 BaseFreq=60 
~ rmatrix = (1.3238 | 0.2066 1.3294 ) 
~ xmatrix = (1.3569 | 0.4591 1.3471 ) 
~ normamps = 230
~ emergamps = 345
~ units=mi 
New linecode.mtx604 nphases=2 BaseFreq=60 
~ rmatrix = (1.3238 | 0.2066 1.3294 ) 
~ xmatrix = (1.3569 | 0.4591 1.3471 ) 
~ normamps = 230
~ emergamps = 345
~ units=mi 
New linecode.mtx605 nphases=1 BaseFreq=60 
~ rmatrix = (1.3292 ) 
~ xmatrix = (1.3475 ) 
~ normamps = 230
~ emergamps = 345
~ units=mi 
New linecode.mtx606 nphases=3 BaseFreq=60 
~ rmatrix = (0.7982 | 0.3192 0.7891 | 0.2849 0.3192 0.7982 ) 
~ xmatrix = (0.4463 | 0.0328 0.4041 | -0.0143 0.0328 0.4463 ) 
~ Cmatrix = [257 | 0 257 | 0 0 257]
~ normamps = 329
~ emergamps = 495
~ units=mi 
New linecode.mtx607 nphases=1 BaseFreq=60 
~ rmatrix = (1.3425 ) 
~ xmatrix = (0.5124 )
~ cmatrix = [236] 
~ normamps = 310
~ emergamps = 465
~ units=mi 


!LOAD DEFINITIONS 
New Load.680a Bus1=680.1     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0
New Load.680b Bus1=680.2     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0
New Load.680c Bus1=680.3     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0

New Load.634a Bus1=634.1     Phases=1 Conn=Wye   Model=1 kV=0.277  kW=0  kvar=0 
New Load.634b Bus1=634.2     Phases=1 Conn=Wye   Model=1 kV=0.277  kW=0  kvar=0 
New Load.634c Bus1=634.3     Phases=1 Conn=Wye   Model=1 kV=0.277  kW=0  kvar=0 

New Load.675a Bus1=675.1     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 
New Load.675b Bus1=675.2     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 
New Load.675c Bus1=675.3     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0

New Load.645b Bus1=645.2     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 
New Load.645c Bus1=645.3     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 

New Load.646b Bus1=646.2     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 
New Load.646c Bus1=646.3     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0

New Load.684a Bus1=684.1     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 
New Load.684c Bus1=684.3     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 

New Load.652a  Bus1=652.1    Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 

New Load.611c  Bus1=611.3    Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0 

New Load.633a Bus1=633.1     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0
New Load.633b Bus1=633.2     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0
New Load.633c Bus1=633.3     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0

New Load.632a Bus1=632.1     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0
New Load.632b Bus1=632.2     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0
New Load.632c Bus1=632.3     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0

New Load.671a Bus1=671.1     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0
New Load.671b Bus1=671.2     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0
New Load.671c Bus1=671.3     Phases=1 Conn=Wye   Model=1 kV=2.4    kW=0  kvar=0


!CAPACITOR DEFINITIONS
!New Capacitor.Cap1 Bus1=675   phases=3 kVAR=600 kV=4.16 
!New Capacitor.Cap2 Bus1=611.3 phases=1 kVAR=100 kV=2.4 

!LINE DEFINITIONS 
New Line.650632    Phases=3 Bus1=RG60.1.2.3   Bus2=632.1.2.3  LineCode=mtx601 Length=2000 units=ft 
New Line.632671    Phases=3 Bus1=632.1.2.3    Bus2=671.1.2.3  LineCode=mtx601 Length=2000 units=ft
New Line.671680    Phases=3 Bus1=671.1.2.3    Bus2=680.1.2.3  LineCode=mtx601 Length=1000 units=ft 
New Line.632633    Phases=3 Bus1=632.1.2.3    Bus2=633.1.2.3  LineCode=mtx602 Length=500  units=ft 
New Line.632645    Phases=2 Bus1=632.3.2      Bus2=645.3.2    LineCode=mtx603 Length=500  units=ft 
New Line.645646    Phases=2 Bus1=645.3.2      Bus2=646.3.2    LineCode=mtx603 Length=300  units=ft 
New Line.671675    Phases=3 Bus1=671.1.2.3    Bus2=675.1.2.3  LineCode=mtx606 Length=500  units=ft 
New Line.671684    Phases=2 Bus1=671.1.3      Bus2=684.1.3    LineCode=mtx604 Length=300  units=ft 
New Line.684611    Phases=1 Bus1=684.3        Bus2=611.3      LineCode=mtx605 Length=300  units=ft 
New Line.684652    Phases=1 Bus1=684.1        Bus2=652.1      LineCode=mtx607 Length=800  units=ft
New Line.675680    Phases=3 Bus1=675.1.2.3    Bus2=680.1.2.3  LineCode=mtx606 Length=500  units=ft  
New Line.611646    Phases=1 Bus1=611.3        Bus2=646.3      LineCode=mtx607 Length=600  units=ft 

Open Line.675680 2

!SWITCH DEFINITIONS 
!New Line.671692    Phases=3 Bus1=671   Bus2=692  Switch=y  r1=1e-4 r0=1e-4 x1=0.000 x0=0.000 c1=0.000 c0=0.000
!Close Line.671692 2

Set Voltagebases=[115, 4.16, .48]
calcv
!Set Controlmode=OFF
!Solve
!BusCoords IEEE13Node_BusXY.csv

!---------------------------------------------------------------------------------------------------------------------------------------------------
!----------------Show some Results -----------------------------------------------------------------------------------------------------------------
!---------------------------------------------------------------------------------------------------------------------------------------------------


// Show Voltages LN Nodes
// Show Currents Elem
// Show Powers kVA Elem
// Show Losses
// Show Taps

!---------------------------------------------------------------------------------------------------------------------------------------------------
!---------------------------------------------------------------------------------------------------------------------------------------------------
! Alternate Solution Script
! To force the taps to be same as published results, set the transformer taps manually and disable the controls
!---------------------------------------------------------------------------------------------------------------------------------------------------


Transformer.Reg1.Taps=[1.0 1.05000]
Transformer.Reg2.Taps=[1.0 1.05000]
Transformer.Reg3.Taps=[1.0 1.06250]

Set Controlmode=OFF

Solve